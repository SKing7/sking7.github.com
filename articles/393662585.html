<!doctype html>
<html>
<head>
<meta charset="utf-8"/>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
<title>iTunes 11 歌曲列表的着色算法是如何实现的？ - 博客</title>
<link rel="stylesheet" href="../css/article.css" />
</head>
<body>
<div class="m-content">
<h1>iTunes 11 歌曲列表的着色算法是如何实现的？ - 博客</h1>
<div><b>本文由 <a href="http://blog.jobbole.com">伯乐在线</a> - <a href="http://blog.jobbole.com/author/jobbole/">伯乐在线读者</a> 翻译自 <a target="_blank" href="http://stackoverflow.com/questions/13637892/how-does-the-algorithm-to-color-the-song-list-in-itunes-11-work">Stackoverflow</a>。转载请参见文章末尾处的要求。</b>
<p>2012年11月份 LuisEspinoza 在Stackoverflow上提问：“最新版的 iTunes 11专辑里歌曲列表非常漂亮，可以从专辑封面给歌曲列表里的字体和背景提取出颜色。有没有人知道这个算法是如何实现的？”</p><p>针对这个问题，Seth Thompson&nbsp;的回答所得投票最高，感谢<strong><a title="袁欣_Jason" href="http://weibo.com/1883102275" target="_blank">@袁欣_Jason</a></strong>&nbsp;的热心翻译。</p><p><img src="http://i.stack.imgur.com/SNXX2.png" alt="Third Example" width="574" height="349" /></p><h3>以下是&nbsp;Seth Thompson&nbsp;的回答</h3><p><img src="http://i.imgur.com/qodsu.png" alt="Example 1" width="574" height="479" /></p><p>在Mathematica中，我用专辑封片图片做为输入，近似模拟出了iTune 11的着色算法：</p><p><img src="http://i.imgur.com/lhVJv.png" alt="Output 1" /></p><p><strong>我是如何做到的</strong></p><p>经过反复尝试，我想出了一种算法，经过我的测试，对大约 80% 的专辑都有效。</p><p><strong>计算颜色的差异</strong></p><p>这个算法就是要寻找图片的主色。能够找到这个主色的先决条件是两种颜色之间的差异是可计量的。其中一种方法就是计算出这两种颜色在RGB颜色空间中的欧式距离（Euclidean distance），不过人对颜色的感知与这种计算出的欧式距离并不匹配。</p><p>因此我写了一个函数，用来将 RGB 颜色转换成&nbsp;<a href="http://en.wikipedia.org/wiki/YUV" target="_blank">YUV</a>&nbsp;格式，这种颜色空间可以更好的模拟人的色觉：</p><pre>convertToYUV[rawRGB_] :=
    Module[{yuv},
        yuv = {{0.299, 0.587, 0.114}, {-0.14713, -0.28886, 0.436},
            {0.615, -0.51499, -0.10001}};
        yuv . rawRGB
    ]</pre><p>接下来，我写了一个函数用来计算转换后两种颜色的色距：</p><pre>ColorDistance[rawRGB1_, rawRGB2_] := 
    EuclideanDistance[convertToYUV @ rawRGB1, convertToYUV @ rawRGB2]</pre><p><strong>寻找主色</strong><strong></strong></p><p>我很快发现，Mathematica的内置函数 DominantColors 做不到像 iTunes 中的算法对细微颜色那么精确的识别。我自己新写了一个函数…</p><p>从一组像素中识别出主色的最简单方法，就是将相似的像素归类，在找出最大的那个色块。</p><pre>DominantColorSimple[pixelArray_] :=
    Module[{buckets},
        buckets = Gather[pixelArray, ColorDistance[#1,#2] &lt; .1 &amp;];
        buckets = Sort[buckets, Length[#1] &gt; Length[#2] &amp;];
        RGBColor @@ Mean @ First @ buckets
    ]</pre><p>注意&nbsp;<code>.1&nbsp;</code>是指将两种颜色区分开的最低色差，虽然我用一个原始像素构成的数组作为输入，为了更好模拟系统内置的<code>DominantColors</code>函数，最后我返回了一个RGBColor类型。</p><p>我真正使用的函数&nbsp;<code>DominantColorsNew</code> 还添加了筛选出某种给定的颜色再返回 n 种主色的功能。你还可以指定区分两种颜色为不同的最低色差。</p><pre>DominantColorsNew[pixelArray_, threshold_: .1, n_: 1, 
    numThreshold_: .2, filterColor_: 0, filterThreshold_: .5] :=
    Module[
        {buckets, color, previous, output},
        buckets = Gather[pixelArray, ColorDistance[#1, #2] &lt; threshold &amp;];
        If[filterColor =!= 0, 
        buckets = 
            Select[buckets, 
                ColorDistance[ Mean[#1], filterColor] &gt; filterThreshold &amp;]];
        buckets = Sort[buckets, Length[#1] &gt; Length[#2] &amp;];
        If[Length @ buckets == 0, Return[{}]];
        color = Mean @ First @ buckets;
        buckets = Drop[buckets, 1];
        output = List[RGBColor @@ color];
        previous = color;
        Do[
            If[Length @ buckets == 0, Return[output]];
            While[
                ColorDistance[(color = Mean @ First @ buckets), previous] &lt; 
                    numThreshold, 
                If[Length @ buckets != 0, buckets = Drop[buckets, 1], 
                    Return[output]]
            ];
            output = Append[output, RGBColor @@ color];
            previous = color,
            {i, n - 1}
        ];
        output
    ]</pre><p align="left"><strong>算法其他内容</strong><strong></strong></p><p align="left">首先我把专辑的封面图片调整为(36px, 36px)，用双边过滤器降低了一些图片细节。</p><pre>image = Import["http://i.imgur.com/z2t8y.jpg"]
thumb = ImageResize[ image, 36, Resampling -&gt; "Nearest"];
thumb = BilateralFilter[thumb, 1, .2, MaxIterations -&gt; 2];</pre><p align="left">iTunes会从专辑的边缘寻找主色给歌曲列表背景着色，有一点要注意的是，计算时要去除掉专辑封面的一圈窄边框。</p><p align="left">接下来我用上面的函数再找出图片最外层主色，使用默认最小色差<code>.1 。</code></p><pre>border = Flatten[

    Join[ImageData[thumb][[1 ;; 34 ;; 33]] ,

        Transpose @ ImageData[thumb][[All, 1 ;; 34 ;; 33]]], 1];

background = DominantColorsNew[border][[1]];</pre><p align="left">最后，我用整张图片作为输入，剔除掉上面找出的背景颜色，再找出两种主色。</p><pre>highlights = DominantColorsNew[Flatten[ImageData[thumb], 1], .1, 2, .2,

    List @@ background, .5];

title = highlights[[1]];

songs = highlights[[2]];</pre><p align="left">.1是指将两种颜色区分对待的最小色差，.2是指找出的主色之间的最小色差，.5指的是主色跟背景颜色之间的最小色差（值越大对比度越高）</p><pre>Graphics[{background, Disk[]}]
Graphics[{title, Disk[]}]
Graphics[{songs, Disk[]}]</pre><p align="left"><img src="http://i.imgur.com/VIufx.png" alt="Final Output" /></p><p align="left"><strong>注释说明</strong><strong></strong></p><p>这个算法对大部分专辑图片都有效。我用上面的色差参数测试，约80%的专辑封面都可以生成正确的颜色。有一些是因为专辑封面是黑白的，导致无法返回高亮色。我的算法没有处理这些特例，不过能不能完全模拟iTunes并不重要。如果一个专辑封面少于两种高亮色，专辑标题会变成黑色或者白色，这取决于跟背景的最佳对比度。歌曲的标题会变成其中一种返回的高亮色，如果没有返回的颜色，那么歌曲的标题颜色会有一些融于背景之中。</p><p><strong>其他例子</strong></p><p><img src="http://i.imgur.com/YPxA3.png" alt="More Examples" width="574" height="584" /></p><p>原文链接： <a target="_blank" ref="nofollow" href="http://stackoverflow.com/questions/13637892/how-does-the-algorithm-to-color-the-song-list-in-itunes-11-work">Stackoverflow</a>&nbsp;&nbsp;&nbsp;翻译： <a href="http://blog.jobbole.com"> 伯乐在线 </a> - <a href="http://blog.jobbole.com/author/jobbole/">伯乐在线读者</a><br />译文链接： <a href="http://blog.jobbole.com/42748/">http://blog.jobbole.com/42748/</a><br />[ <span><b>转载必须在正文中标注并保留原文链接、译文链接和译者等信息。</b></span>]</p></div></div>
<script>var _gaq = _gaq || []; _gaq.push(['_setAccount', 'UA-34802167-1']); _gaq.push(['_setDomainName', 'liuzhe.co']); _gaq.push(['_trackPageview']); (function() { var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true; ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js'; var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s); })();</script></body>
</html>